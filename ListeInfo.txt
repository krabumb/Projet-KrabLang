S'          -> ALGORITHME S $
S           -> DEBUT BLOCK FIN
BLOCK       -> BLOCKINFO BLOCK | €
BLOCKINFO   -> DECLARATION | TVF ASSIOUFUNC | SIFSI | TANTQUE
DECLARATION -> TYPEPRIM TVF DECLAPOSS
DECLAPOSS   -> ASSIGNATION | EOI
ASSIGNATION -> EGAL EXPR EOI
ASSIOUFUNC  -> ASSIGNATION | FUNCTIONEXE
SIFSI       -> SI EXPR BLOCK SINONOUPAS FSI
SINONOUPAS  -> SINON BLOCK | €
TANTQUE     -> TANT QUE EXPR BLOCK FTANT
FUNCTIONEXE -> PARENTHESEO ARGUMENT PARENTHESEF EOI
ARGUMENT    -> ARGUMENTN | €
ARGUMENTN   -> EXPR ARGUMENTG
ARGUMENTG   -> VIRGULE ARGUMENTN | €
EXPR        -> PARENTHESEO EXPR PARENTHESEF | EXPR BINOPERATOR EXPR | OPERAND | UNAOPERATOR EXPR
BINOPERATOR -> OPERATOR | OPEGAL EGAL | INFSUP INFSUPOP
OPERAND     -> NUMBER | TVF OPERANDFUNC | NULL | BOOLEAN | STRING
OPERANDFUNC -> FUNCTIONEXE | €
NUMBER      -> ENTIER | DECIMAL
UNAOPERATOR -> NON | PLUSMINUS
OPEGAL      -> NON | EGAL
INFSUPOP    -> EGAL | €
OPERATOR    -> PLUSMINUS | COMPMATHOP | BOOLOP

ALGORITHME  -> "Algorithme"
DEBUT       -> "debut"
FIN         -> "fin"
EGAL        -> "="
BOOLEAN     -> "true" | "false"
TYPEPRIM    -> "entier" | "decimal" | "chaine" | "booleen"
BOOLOP      -> "et" | "ou"
COMPMATHOP  -> "/" | "*" | "%"
INFSUP      -> "<" | ">"
NULL        -> "null"
PLUSMINUS   -> "+" | "-"
NON         -> "non"
PARENTHESEO -> "("
PARENTHESEF -> ")"
EOI         -> ";"
VIRGULE     -> ","
SI          -> "si"
SINON       -> "sinon"
FSI         -> "fsi"
TANT        -> "tant"
QUE         -> "que"
FTANT       -> "ftant"

ENTIER      -> [0-9]+
DECIMAL     -> [0-9]*\.[0-9]+
TVF         -> [A-Za-z][A-Za-z0-9_]*
STRING      -> \".*\"